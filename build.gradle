plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'portfolio'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {

	// dev
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	testImplementation 'org.springframework.security:spring-security-test'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// data
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.1'

	// product
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // lombok
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

    // rest docs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // image resize
	implementation 'org.imgscalr:imgscalr-lib:4.2'

	// emil
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// excel
	implementation 'org.apache.poi:poi:5.2.2'
	implementation 'org.apache.poi:poi-ooxml:5.2.3'
	implementation 'commons-io:commons-io:2.11.0'

	// aws s3 api for naver cloud
	// implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
	// implementation 'com.amazonaws:aws-java-sdk-s3:1.12.13'

	// firebase admin sdk
	// implementation 'com.google.firebase:firebase-admin:9.2.0'
}

// test 및 rest docs 추가 시작
tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	sources {
		include('docs/**/*.adoc')
		exclude('docs/snippets/**/*.adoc')
	}
	baseDirFollowsSourceDir()
	dependsOn test
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}
// test 및 rest docs 추가 끝

// query dsl 추가 시작
clean {
	delete file('src/main/generated')
}
// query dsl 추가 끝
